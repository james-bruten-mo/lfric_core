!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Test the Lagrangian trapezoidal departure point calculation

module trapezoidal_deppt_kernel_mod_test

  use constants_mod, only : i_def, r_def
  use pFUnit_Mod

  implicit none

  private

  public :: test_all

  @TestCase
  type, public, extends(TestCase) :: trapezoidal_deppt_test_type
    private
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type trapezoidal_deppt_test_type

contains
 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,            only : feign_departure_points_config
    use departure_points_config_mod, only : departure_grid_physical, &
                                            method_trapezoidal,      &
                                            vertical_limit_boundary, &
                                            vertical_method_trapezoidal

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    call feign_departure_points_config( departure_grid = departure_grid_physical,     &
                                        method = method_trapezoidal,                  &
                                        n_dep_pt_iterations = 1,                      &
                                        vertical_limit = vertical_limit_boundary,     &
                                        vertical_method = vertical_method_trapezoidal &
                                      )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    call final_configuration()

  end subroutine tearDown
  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  @Test
  subroutine test_all( this )

    use, intrinsic :: iso_fortran_env, only: real64
    use trapezoidal_deppt_kernel_mod,  only: trapezoidal_deppt_code

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    real(kind=r_def), parameter :: tol = 1.0e-12_r_def   ! r_def 64-bit
    real(kind=r_def) :: answer, use_tol

    integer(kind=i_def), parameter :: nlayers = 1
    integer(kind=i_def), parameter :: ndf_w2 = 4
    integer(kind=i_def), parameter :: stencil_size = 5
    integer(kind=i_def), parameter :: undf_w2  = ndf_w2*nlayers
    integer(kind=i_def), parameter :: undf_w2s = ndf_w2*nlayers*stencil_size

    integer(kind=i_def), dimension(ndf_w2)              :: map_w2
    integer(kind=i_def), dimension(ndf_w2,stencil_size) :: stencil_map

    real(kind=r_def), dimension(undf_w2)  :: dep_pts_x
    real(kind=r_def), dimension(undf_w2)  :: dep_pts_y
    real(kind=r_def), dimension(undf_w2s) :: wind_n
    real(kind=r_def), dimension(undf_w2)  :: wind_np1

    real(kind=r_def)    :: dt
    integer(kind=i_def) :: n_it

    ! Set up maps
    map_w2(:) = (/ 1, 2, 3, 4 /)
    ! Cross stencil has form
    !       5
    !     2 1 4
    !       3
    stencil_map(1,:) = (/ 1, 2, 3, 4, 5 /)
    stencil_map(2,:) = (/ 6, 7, 8, 9, 10 /)
    stencil_map(3,:) = (/ 11, 12, 13, 14, 15 /)
    stencil_map(4,:) = (/ 16, 17, 18, 19, 20 /)

    ! Initialise output as zero
    dep_pts_x(:) = 0.0_r_def
    dep_pts_y(:) = 0.0_r_def

    ! Set up winds to be constant
    wind_n(:) = 1.0_r_def
    wind_np1(:) = 1.0_r_def
    dt = 1.0_r_def
    n_it = 1_i_def
    call trapezoidal_deppt_code( nlayers,      &
                                 dep_pts_x,    &
                                 dep_pts_y,    &
                                 wind_n,       &
                                 stencil_size, &
                                 stencil_map,  &
                                 wind_np1,     &
                                 n_it,         &
                                 dt,           &
                                 ndf_w2,       &
                                 undf_w2,      &
                                 map_w2 )
    ! Get correct tolerance
    if ( r_def == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_def*spacing( maxval( dep_pts_x(1:4) ) )
    end if
    answer = 1.0_r_def
    @assertEqual(answer, dep_pts_x(1), use_tol)
    @assertEqual(answer, dep_pts_x(3), use_tol)
    answer = -1.0_r_def
    @assertEqual(answer, dep_pts_y(2), use_tol)
    @assertEqual(answer, dep_pts_y(4), use_tol)
    ! Check other dofs are zero
    answer = 0.0_r_def
    @assertEqual(answer, dep_pts_x(2), use_tol)
    @assertEqual(answer, dep_pts_x(4), use_tol)
    @assertEqual(answer, dep_pts_y(1), use_tol)
    @assertEqual(answer, dep_pts_y(3), use_tol)

    ! Set up winds to vary
    wind_n(:) = (/ 1.0_r_def, 3.0_r_def, 4.0_r_def, 2.0_r_def, 4.0_r_def, &
                   1.0_r_def, 4.0_r_def, 4.0_r_def, 4.0_r_def, 2.0_r_def, &
                   2.0_r_def, 1.0_r_def, 4.0_r_def, 4.0_r_def, 5.0_r_def, &
                   2.0_r_def, 4.0_r_def, 1.0_r_def, 4.0_r_def, 0.0_r_def  /)
    wind_np1(:) = 1.0_r_def
    dt = 0.5_r_def
    n_it = 1_i_def
    call trapezoidal_deppt_code( nlayers,      &
                                 dep_pts_x,    &
                                 dep_pts_y,    &
                                 wind_n,       &
                                 stencil_size, &
                                 stencil_map,  &
                                 wind_np1,     &
                                 n_it,         &
                                 dt,           &
                                 ndf_w2,       &
                                 undf_w2,      &
                                 map_w2 )
    answer = 0.75_r_def
    @assertEqual(answer, dep_pts_x(1), use_tol)
    answer = 0.5_r_def
    @assertEqual(answer, dep_pts_x(3), use_tol)
    answer = -0.625_r_def
    @assertEqual(answer, dep_pts_y(2), use_tol)
    answer = -0.25_r_def
    @assertEqual(answer, dep_pts_y(4), use_tol)

  end subroutine test_all

end module trapezoidal_deppt_kernel_mod_test
