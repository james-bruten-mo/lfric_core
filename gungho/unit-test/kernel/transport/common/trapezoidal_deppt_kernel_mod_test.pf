!-----------------------------------------------------------------------------
! (c) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Test the Lagrangian trapezoidal departure point calculation

module trapezoidal_deppt_kernel_mod_test

  use, intrinsic :: iso_fortran_env, only: real64

  use constants_mod,                       only : i_def, r_def, r_tran
  use get_unit_test_m3x3_dofmap_mod,       only : get_w2_m3x3_dofmap, &
                                                  get_m3x3_stencil_dofmap_cross
  use get_unit_test_m3x3_q3x3x3_sizes_mod, only : get_w2_m3x3_q3x3x3_size
  use pFUnit_Mod
  use trapezoidal_deppt_kernel_mod,        only: trapezoidal_deppt_code

  implicit none

  private

  public :: test_unitary_wind_field, test_varying_wind_field

  @TestCase
  type, public, extends(TestCase) :: trapezoidal_deppt_test_type
    private
    integer(kind=i_def)              :: ndf_w2
    integer(kind=i_def)              :: undf_w2
    integer(kind=i_def), allocatable :: dofmap_w2(:, :)
    integer(kind=i_def), allocatable :: stencil_map_w2(:, :, :)

    real(kind=r_tran), allocatable :: wind_n(:)
    real(kind=r_tran), allocatable :: wind_np1(:)
    real(kind=r_tran), allocatable :: dep_pts_x(:)
    real(kind=r_tran), allocatable :: dep_pts_y(:)
  contains
    private
    procedure, public :: setUp
    procedure, public :: tearDown
    procedure, public :: test_unitary_wind_field
    procedure, public :: test_varying_wind_field
  end type trapezoidal_deppt_test_type

  integer(kind=i_def), parameter :: nlayers = 1
  integer(kind=i_def), parameter :: stencil_size = 5
  real(kind=r_tran),    parameter :: tol = 1.0e-12_r_tran   ! r_tran 64-bit

contains

 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use feign_config_mod,            only : feign_departure_points_config
    use departure_points_config_mod, only : departure_grid_physical, &
                                            horizontal_limit_none,   &
                                            method_trapezoidal,      &
                                            vertical_limit_boundary, &
                                            vertical_method_trapezoidal

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    integer(kind=i_def) :: ncells_w2
    integer(kind=i_def) :: dim_space_w2, dim_space_diff_w2
    integer(kind=i_def) :: nqp_h_w2, nqp_v_w2

    call feign_departure_points_config(             &
      departure_grid = departure_grid_physical,     &
      horizontal_limit = horizontal_limit_none,     &
      method = method_trapezoidal,                  &
      n_dep_pt_iterations = 1,                      &
      vertical_limit = vertical_limit_boundary,     &
      vertical_method = vertical_method_trapezoidal &
    )

    call get_w2_m3x3_q3x3x3_size( this%ndf_w2, this%undf_w2, ncells_w2, &
                                  dim_space_w2, dim_space_diff_w2,      &
                                  nqp_h_w2, nqp_v_w2, nlayers )
    call get_w2_m3x3_dofmap( this%dofmap_w2, nlayers )
    call get_m3x3_stencil_dofmap_cross( this%stencil_map_w2, this%dofmap_w2 )

    allocate( this%wind_n(this%undf_w2) )
    allocate( this%wind_np1(this%undf_w2) )
    allocate( this%dep_pts_x(this%undf_w2) )
    allocate( this%dep_pts_y(this%undf_w2) )

    ! Initialise output as zero
    this%dep_pts_x(:) = 0.0_r_tran
    this%dep_pts_y(:) = 0.0_r_tran

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    use configuration_mod, only: final_configuration

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    if (allocated( this%dep_pts_y ))      deallocate( this%dep_pts_y )
    if (allocated( this%dep_pts_x ))      deallocate( this%dep_pts_x )
    if (allocated( this%wind_np1 ))       deallocate( this%wind_np1 )
    if (allocated( this%wind_n ))         deallocate( this%wind_n )
    if (allocated( this%stencil_map_w2 )) deallocate( this%stencil_map_w2 )
    if (allocated( this%dofmap_w2 ))      deallocate( this%dofmap_w2 )

    call final_configuration()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_unitary_wind_field( this )

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    real(kind=r_tran),    parameter :: dt = 0.5_r_tran
    integer(kind=i_def), parameter :: n_it = 1_i_def

    real(kind=r_tran) :: use_tol
    real(kind=r_tran) :: slice(3)

    real(kind=r_tran), allocatable :: expected(:)

    ! Set up winds to be constant
    this%wind_n(:) = 1.0_r_tran
    this%wind_np1(:) = 1.0_r_tran

    ! Pass column 5 as this is the centre of the 3x3 mesh.
    !
    call trapezoidal_deppt_code( nlayers,             &
                                 this%dep_pts_x,      &
                                 this%dep_pts_y,      &
                                 this%wind_n,         &
                                 stencil_size,        &
                                 this%stencil_map_w2(:,:,5), &
                                 this%wind_np1,       &
                                 n_it,                &
                                 dt,                  &
                                 this%ndf_w2,         &
                                 this%undf_w2,        &
                                 this%dofmap_w2(:, 5) )

    slice(1) = this%dep_pts_x(16)
    slice(2) = this%dep_pts_x(17)
    slice(3) = this%dep_pts_x(21)

    ! Get correct tolerance
    if ( r_tran == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_tran*spacing( maxval( slice ) )
    end if

    expected = (/0.0_r_tran, 0.5_r_tran, 0.5_r_tran/)
    @assertEqual( expected, slice, use_tol )

    slice(1) = this%dep_pts_y(7)
    slice(2) = this%dep_pts_y(9)
    slice(3) = this%dep_pts_y(22)
    expected = (/0.0_r_tran, -0.5_r_tran, -0.5_r_tran/)
    @assertEqual( expected, slice, use_tol )

  end subroutine test_unitary_wind_field

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test
  subroutine test_varying_wind_field( this )

    implicit none

    class(trapezoidal_deppt_test_type), intent(inout) :: this

    real(kind=r_tran),    parameter :: dt = 0.4_r_tran
    integer(kind=i_def), parameter :: n_it = 1_i_def

    real(kind=r_tran) :: use_tol
    real(kind=r_tran) :: slice(3)

    real(kind=r_tran), allocatable :: expected(:)

    ! Set up winds to vary
    this%wind_n = 0.0_r_tran
    this%wind_n(7) = 4.0_r_tran
    this%wind_n(9) = 1.0_r_tran
    this%wind_n(22) = 2.0_r_tran
    this%wind_n(16) = 3.0_r_tran
    this%wind_n(17) = 1.0_r_tran
    this%wind_n(21) = 2.0_r_tran

    this%wind_np1 = 1.0_r_tran

    ! Pass column 5 as this is the centre of the 3x3 mesh.
    !
    call trapezoidal_deppt_code( nlayers,                    &
                                 this%dep_pts_x,             &
                                 this%dep_pts_y,             &
                                 this%wind_n,                &
                                 stencil_size,               &
                                 this%stencil_map_w2(:,:,5), &
                                 this%wind_np1,              &
                                 n_it,                       &
                                 dt,                         &
                                 this%ndf_w2,                &
                                 this%undf_w2,               &
                                 this%dofmap_w2(:,5) )

    slice(1) = this%dep_pts_x(16)
    slice(2) = this%dep_pts_x(17)
    slice(3) = this%dep_pts_x(21)

    ! Get correct tolerance
    if ( r_tran == real64 ) then
      use_tol = tol
    else
      use_tol = 10.0_r_tran*spacing( maxval( slice ) )
    end if

    expected = (/0.0_r_tran, 0.56_r_tran, 0.44_r_tran/)
    @assertEqual( expected, slice, use_tol )

  end subroutine test_varying_wind_field

end module trapezoidal_deppt_kernel_mod_test
