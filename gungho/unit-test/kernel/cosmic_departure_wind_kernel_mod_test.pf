!-----------------------------------------------------------------------------
! (C) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------

!> Test the calculation the winds required for the Cosmic transport scheme from
!> the Piola winds used in GungHo. The values are multiplied by detJ at the W2
!> dof locations.

module cosmic_departure_wind_kernel_mod_test

  use constants_mod,                 only : i_def, r_def
  use function_space_collection_mod, only : function_space_collection_type, &
                                            function_space_collection
  use mesh_collection_mod,           only : mesh_collection_type, &
                                            mesh_collection
  use field_mod,                     only : field_type,      &
                                            field_proxy_type
  use fs_continuity_mod,             only : W2
  use function_space_mod,            only : function_space_type
  use flux_direction_mod,            only : x_direction, y_direction
  use yaxt,                          only : xt_initialize, xt_finalize
  use mpi_mod,                       only : store_comm
  use pFUnit_Mod

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: cosmic_departure_wind_test_type
    private
    type(field_type)         :: piola_field
    type(field_type)         :: departure_field
    type(field_type)         :: detj_field
    real(r_def), allocatable :: nodal_basis_u(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type cosmic_departure_wind_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(cosmic_departure_wind_test_type), intent(inout) :: this

    type(function_space_type) :: fs
    type(field_proxy_type)    :: u_piola
    type(field_proxy_type)    :: detj_at_w2

    type(function_space_type), pointer :: w2_fs => null()

    integer(i_def) :: ndf
    integer(i_def) :: dim_u
    integer(i_def) :: mesh_id

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    w2_fs => function_space_collection%get_fs( mesh_id, element_order, W2 )
    this%piola_field =  field_type( vector_space = w2_fs )
    this%departure_field =  field_type( vector_space = w2_fs )
    this%detj_field =  field_type( vector_space = w2_fs )
    u_piola = this%piola_field%get_proxy()
    ndf  = u_piola%vspace%get_ndf()

    dim_u  = u_piola%vspace%get_dim_space( )
    allocate( this%nodal_basis_u(dim_u, ndf, ndf) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(cosmic_departure_wind_test_type), intent(inout) :: this

    deallocate( this%nodal_basis_u )

    call function_space_collection%clear()
    call mesh_collection%clear()

    ! Finalise YAXT
    call xt_finalize()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use cosmic_departure_wind_kernel_mod, only: cosmic_departure_wind_code
    use function_space_mod,             only: BASIS, DIFF_BASIS

    implicit none

    class(cosmic_departure_wind_test_type), intent(inout) :: this

    type(field_proxy_type)    :: u_piola, u_departure_wind
    type(field_proxy_type)    :: detj_at_w2

    real(kind=r_def)          :: answer, physical_wind_value
    real(r_def), parameter    :: tol = 1.0e-12_r_def

    integer                   :: i, j, k, cell, df
    integer                   :: nlayers
    integer                   :: ndf_u, ndf_udep
    integer                   :: df_u, df_udep
    integer                   :: undf_u
    integer                   :: dim_u
    integer                   :: element_order
    integer, pointer          :: map(:) => null()
    real(kind=r_def), pointer :: nodes_udep(:,:) => null()


    u_piola = this%piola_field%get_proxy()
    u_departure_wind = this%departure_field%get_proxy()
    detj_at_w2 = this%detj_field%get_proxy()

    nlayers = u_piola%vspace%get_nlayers()

    ndf_u  = u_piola%vspace%get_ndf()
    undf_u = u_piola%vspace%get_undf()
    dim_u  = u_piola%vspace%get_dim_space( )

    ndf_udep   =  u_departure_wind%vspace%get_ndf()
    nodes_udep => u_departure_wind%vspace%get_nodes()


    ! Evaluate the basis function
    do df_udep = 1, ndf_udep
      do df_u = 1, ndf_u
        this%nodal_basis_u(:,df_u,df_udep) = u_piola%vspace%call_function(BASIS,df_u,nodes_udep(:,df_udep))
      end do
    end do

    cell = 1
    map     => u_piola%vspace%get_cell_dofmap( cell )

    physical_wind_value = 3.0_r_def
    detj_at_w2%data(:) = 12.0_r_def

    do df =  1,ndf_u
      u_piola%data(map(df)) = physical_wind_value*dot_product(this%nodal_basis_u(:,df,df),abs(this%nodal_basis_u(:,df,df)))
    end do
    u_departure_wind%data(:) = 0.0_r_def


    call cosmic_departure_wind_code(nlayers,                                  &
                                    u_departure_wind%data,                    &
                                    u_piola%data,                             &
                                    detj_at_w2%data,                          &
                                    ndf_u, undf_u, map,                       &
                                    x_direction                               &
                                     )

    answer = physical_wind_value/(12.0_r_def)

    @assertEqual(answer, u_departure_wind%data(map(1)), tol)
    @assertEqual(-answer, u_departure_wind%data(map(2)), tol)
    @assertEqual(answer, u_departure_wind%data(map(3)), tol)
    @assertEqual(-answer, u_departure_wind%data(map(4)), tol)

    call cosmic_departure_wind_code(nlayers,                                  &
                                    u_departure_wind%data,                    &
                                    u_piola%data,                             &
                                    detj_at_w2%data,                          &
                                    ndf_u, undf_u, map,                       &
                                    y_direction                               &
                                     )

    @assertEqual(-answer, u_departure_wind%data(map(1)), tol)
    @assertEqual(answer, u_departure_wind%data(map(2)), tol)
    @assertEqual(-answer, u_departure_wind%data(map(3)), tol)
    @assertEqual(answer, u_departure_wind%data(map(4)), tol)


  end subroutine test_all

end module cosmic_departure_wind_kernel_mod_test
