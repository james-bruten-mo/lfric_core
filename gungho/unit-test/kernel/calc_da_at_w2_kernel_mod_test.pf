!-----------------------------------------------------------------------------
! Copyright (c) 2017,  Met Office, on behalf of HMSO and Queen's Printer
! For further details please refer to the file LICENCE.original which you
! should have received as part of this distribution.
!-----------------------------------------------------------------------------

!> Test the computation of face areas
!>
module calc_dA_at_w2_kernel_mod_test

    use constants_mod,                 only : i_def, r_def
    use function_space_collection_mod, only : function_space_collection_type, &
                                              function_space_collection
    use mesh_collection_mod,           only : mesh_collection_type, &
                                              mesh_collection
    use field_mod,                     only : field_type,field_proxy_type
    use fs_continuity_mod,             only : W0, W2
    use function_space_mod,            only : function_space_type, DIFF_BASIS
    use pFUnit_Mod
    use reference_element_mod,         only : N,S,E,W,T,B
    use yaxt,                          only : xt_initialize, xt_finalize
    use mpi_mod,                       only : store_comm

  implicit none

  private
  public :: test_all

  @TestCase
  type, extends(MPITestCase), public :: calc_dA_at_w2_test_type
    private
    type(function_space_type), pointer :: w0_fs => null()
    type(function_space_type), pointer :: w2_fs => null()
    type(field_type)                   :: dA_w2
    type(field_type)                   :: chi(3)
    integer(i_def)                     :: mesh_id
    real(r_def), allocatable           :: diff_basis_chi(:,:,:)
  contains
    procedure setUp
    procedure tearDown
    procedure test_all
  end type calc_dA_at_w2_test_type

  integer(i_def), parameter :: element_order = 0

contains

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine setUp( this )

    use finite_element_config_mod, only : finite_element_cellshape_quadrilateral
    use mesh_mod,                  only : PLANE_BI_PERIODIC

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    type(field_proxy_type)    :: chi_p(3)
    type(field_proxy_type)    :: dA_w2_p

    integer(kind=i_def) :: nlayers, dim, diff_dim
    integer(kind=i_def) :: ndf_chi, ndf_w2

    integer(kind=i_def) :: i

    ! Initialise YAXT
    call xt_initialize(this%getMpiCommunicator())
    !Store the MPI communicator for later use
    call store_comm(this%getMpiCommunicator())

    ! Create top level mesh collection
    mesh_collection = mesh_collection_type()
    ! Create top level function space collection
    function_space_collection = function_space_collection_type()

    ! Dummy mesh mod has 9 cells, 3 layers and is uniform in vertical
    this%mesh_id = mesh_collection%add_unit_test_mesh( PLANE_BI_PERIODIC )

    this%w0_fs => function_space_collection%get_fs( this%mesh_id, &
                                                      element_order, &
                                                      W0 )
    this%w2_fs => function_space_collection%get_fs( this%mesh_id, &
                                                    element_order, &
                                                    W2 )

    do i = 1,3
      this%chi(i) = field_type( vector_space = this%w0_fs )
      chi_p(i) = this%chi(i)%get_proxy()
    end do

    diff_dim = chi_p(1)%vspace%get_dim_space_diff( )
    ndf_chi  = chi_p(1)%vspace%get_ndf()
 
    this%dA_w2 = field_type( vector_space = this%w2_fs )
    dA_w2_p    = this%dA_w2%get_proxy()
    ndf_w2     = dA_w2_p%vspace%get_ndf()

    allocate(this%diff_basis_chi(diff_dim, ndf_chi, ndf_w2) )

  end subroutine setUp

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  subroutine tearDown( this )

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    ! Finalise YAXT
    call xt_finalize()

    deallocate( this%diff_basis_chi)

    call mesh_collection%clear()
    call function_space_collection%clear()

  end subroutine tearDown

  !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  @Test( npes=[1] )
  subroutine test_all( this )

    use calc_dA_at_w2_kernel_mod, only : calc_dA_at_w2_code
    use mesh_mod,                 only : mesh_type

    implicit none

    class(calc_dA_at_w2_test_type), intent(inout) :: this

    type(field_proxy_type)    :: dA_w2_p
    type(field_proxy_type )   :: chi_p(3)

    real(kind=r_def), parameter :: tol = 1.0e-8_r_def
    real(kind=r_def), parameter :: dx = 4.0_r_def, &
                                   dy = 7.0_r_def, &
                                   dz = 13.0_r_def

    real(kind=r_def), pointer :: nodes_w2(:,:) => null()

    type(mesh_type),pointer   :: mesh => null()

    integer(kind=i_def) :: i, j, k, cell, df_w2, df_chi

    integer(kind=i_def) :: nlayers
    integer(kind=i_def) :: ndf_chi, undf_chi, ndf_w2, undf_w2

    integer(kind=i_def), pointer                 :: map_chi(:)  => null()
    integer(kind=i_def), pointer                 :: map_w2(:)  => null()

    real(kind=r_def) :: dzdx = .75 ! slope to mimic orography

    mesh => mesh_collection%get_mesh( this%mesh_id )

    ! Make a coordinate field with a function space
    do i = 1,3
      chi_p(i) = this%chi(i)%get_proxy()
    end do
    ! Compute coordinates
    cell = 1
    do j = 1,3
      do i = 1,3
        map_chi => chi_p(1)%vspace%get_cell_dofmap( cell )
        do k = 0,3
          chi_p(1)%data(map_chi(1) + k) = real(i-1)*dx
          chi_p(2)%data(map_chi(1) + k) = real(j-1)*dy
          chi_p(3)%data(map_chi(1) + k) = real(k)  *dz + real(i-1)*dzdx*dx
        end do
        cell = cell + 1
      end do
    end do

    dA_w2_p     = this%dA_w2%get_proxy()
    ndf_w2      = dA_w2_p%vspace%get_ndf()
    undf_w2     = dA_w2_p%vspace%get_undf()

    nlayers     = chi_p(1)%vspace%get_nlayers()
    ndf_chi     = chi_p(1)%vspace%get_ndf()
    undf_chi    = chi_p(1)%vspace%get_undf()
    nodes_w2    => da_w2_p%vspace%get_nodes()

    do df_w2=1,ndf_w2
      do df_chi=1,ndf_chi
        this%diff_basis_chi(:,df_chi,df_w2) = &
           chi_p(1)%vspace%call_function(DIFF_BASIS,df_chi,nodes_w2(:,df_w2))
      end do
    end do

    cell = 1
    map_chi => chi_p(1)%vspace%get_cell_dofmap(cell)
    map_w2 => dA_w2_p%vspace%get_cell_dofmap(cell)

    call calc_dA_at_w2_code( nlayers,                          &
                             dA_w2_p%data,                     &
                             chi_p(1)%data,                    &
                             chi_p(2)%data,                    &
                             chi_p(3)%data,                    &
                             ndf_w2,                           &
                             undf_w2,                          &
                             map_w2,                           &
                             ndf_chi,                          &
                             undf_chi,                         &
                             map_chi,                          &
                             this%diff_basis_chi               )

    ! dx = 4, dy = 7, dz = 13, dzdx=.75, hence areas are
    ! N,S: 7*13 = 91
    ! E,W: 4*13 = 52
    ! T,B: 5*7  = 35
    @assertEqual(91.0_r_def, dA_w2_p%data(map_w2(N)), tol)
    @assertEqual(91.0_r_def, dA_w2_p%data(map_w2(S)), tol)
    @assertEqual(52.0_r_def, dA_w2_p%data(map_w2(E)), tol)
    @assertEqual(52.0_r_def, dA_w2_p%data(map_w2(W)), tol)
    @assertEqual(35.0_r_def, dA_w2_p%data(map_w2(T)), tol)
    @assertEqual(35.0_r_def, dA_w2_p%data(map_w2(B)), tol)

  end subroutine test_all

end module calc_dA_at_w2_kernel_mod_test
