!-------------------------------------------------------------------------------
! (c) Crown copyright 2017 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-------------------------------------------------------------------------------
!> @brief Interface to the implicit UM Boundary Layer scheme

module bl_imp_alg_mod

  use constants_mod,                 only: i_def
  use clock_mod,                     only: clock_type
  use diagnostics_io_mod,            only: write_vector_diagnostic
  use field_mod,                     only: field_type
  use field_collection_mod,          only: field_collection_type
  use fs_continuity_mod,             only: W1, W2, W3, Wtheta
  use geometric_constants_mod,       only: get_height, get_da_at_w2
  use physical_op_constants_mod,     only: get_rdz_fd1, get_dtrdz_fd2
  use mr_indices_mod,                only: nummr, imr_v, imr_cl, imr_ci
  use timestepping_config_mod,       only: outer_iterations

  use io_config_mod, only: subroutine_timers
  use timer_mod,     only: timer
  ! xios output
  use io_config_mod, only: write_diag, use_xios_io
  use log_mod,              only: log_event, LOG_LEVEL_INFO

  use map_fd_to_prognostics_alg_mod, only: set_wind
  use set_any_dof_kernel_mod,        only: set_any_dof_kernel_type
  use reference_element_mod,         only: W, S, E, N
  use physics_mappings_alg_mod,      only: map_physics_winds
  use convection_config_mod,         only: cv_scheme, cv_scheme_gregory_rowntree
  use blayer_config_mod,             only: fric_heating

  implicit none

  private
  public bl_imp_alg

contains

  !>@brief Run the implicit UM Boundary Layer scheme
  !>@details The UM Boundary Layer scheme does:
  !>             vertical mixing of heat, momentum and moisture,
  !>             as documented in UMDP24
  !>         NB This version uses winds in w3 space (i.e. A-grid)
  !>@param[in,out] dtheta_bl         Theta increment
  !>@param[in,out] du_bl             3D wind increment
  !>@param[in,out] mr                Mixing ratios
  !>@param[out]    snow_sublimation  Sublimation of snow
  !>@param[out]    surf_heat_flux    Net surface heat flux
  !>@param[out]    canopy_evap       Canopy evaporation from land tiles
  !>@param[out]    water_extraction  Extraction of water from each soil layer
  !>@param[out]    total_snowmelt    Surface plus canopy snowmelt rate
  !>@param[in]     theta             Theta in its native (wth) space
  !>@param[in]     exner             Exner Pressure in the w3 space
  !>@param[in]     mr_n              Mixing ratios at time level n
  !>@param[in]     derived_fields    Group of derived fields
  !>@param[in]     radition_fields   Fields for radiation scheme
  !>@param[in]     orography_fields  Fields for orog drag scheme
  !>@param[in,out] turbulence_fields Fields for turbulence scheme
  !>@param[in]     convection_fields Fields for convection scheme
  !>@param[in,out] cloud_fields      Fields for cloud scheme
  !>@param[in,out] surface_fields    Fields for surface scheme
  !>@param[in]     soil_fields       Fields for soil hydrology scheme
  !>@param[in]     snow_fields       Fields for snow scheme
  !>@param[in]     outer             Outer loop counter
  !>@param[in]     clock             The clock object
  subroutine bl_imp_alg(dtheta_bl, du_bl, mr, snow_sublimation,          &
                        surf_heat_flux, canopy_evap, water_extraction,   &
                        total_snowmelt, theta, exner, mr_n,              &
                        derived_fields, radiation_fields,                &
                        orography_fields, turbulence_fields,             &
                        convection_fields, cloud_fields, surface_fields, &
                        soil_fields, snow_fields, outer, clock)

    use bl_imp_kernel_mod, only: bl_imp_kernel_type
    use bl_imp_du_kernel_mod, only: bl_imp_du_kernel_type

    implicit none

    type( field_type ), intent( inout )     :: dtheta_bl, du_bl, mr(nummr)
    type( field_type ), intent( out )       :: snow_sublimation,              &
                                               surf_heat_flux, canopy_evap,   &
                                               water_extraction, total_snowmelt
    type( field_type ), intent( in )        :: theta, exner, mr_n(nummr)

    type( field_collection_type ), intent(in)    :: derived_fields
    type( field_collection_type ), intent(in)    :: radiation_fields
    type( field_collection_type ), intent(in)    :: orography_fields
    type( field_collection_type ), intent(inout) :: turbulence_fields
    type( field_collection_type ), intent(in)    :: convection_fields
    type( field_collection_type ), intent(inout) :: cloud_fields
    type( field_collection_type ), intent(inout) :: surface_fields
    type( field_collection_type ), intent(in)    :: soil_fields
    type( field_collection_type ), intent(in)    :: snow_fields
    integer(kind=i_def), intent(in) :: outer
    class(clock_type),   intent(in) :: clock

    ! Temporary fields unpacked fields from collections
    type( field_type ), pointer :: exner_in_wth  => null()
    type( field_type ), pointer :: wetrho_in_wth => null()
    type( field_type ), pointer :: wetrho_in_w3 => null()
    type( field_type ), pointer :: wetrho_in_w2  => null()
    type( field_type ), pointer :: theta_star => null()
    type( field_type ), pointer :: u_physics => null()
    type( field_type ), pointer :: u_physics_star => null()

    type( field_type ), pointer :: zh => null()
    type( field_type ), pointer :: zhsc => null()
    type( field_type ), pointer :: ntml => null()
    type( field_type ), pointer :: cumulus => null()
    type( field_type ), pointer :: blend_height_tq  => null()
    type( field_type ), pointer :: zh_nonloc  => null()
    type( field_type ), pointer :: bl_type_ind  => null()
    type( field_type ), pointer :: z_lcl => null()
    type( field_type ), pointer :: inv_depth => null()
    type( field_type ), pointer :: qcl_at_inv_top => null()
    type( field_type ), pointer :: rhokh_bl => null()
    type( field_type ), pointer :: bq_bl => null()
    type( field_type ), pointer :: bt_bl => null()
    type( field_type ), pointer :: moist_flux_bl => null()
    type( field_type ), pointer :: heat_flux_bl => null()
    type( field_type ), pointer :: dtrdz_tq_bl => null()
    type( field_type ), pointer :: rdz_tq_bl => null()
    type( field_type ), pointer :: dsldzm => null()
    type( field_type ), pointer :: wvar => null()
    type( field_type ), pointer :: gradrinr => null()
    type( field_type ), pointer :: rhokm_w2 => null()
    type( field_type ), pointer :: tau_w2 => null()

    type( field_type ), pointer :: cf_area  => null()
    type( field_type ), pointer :: cf_ice  => null()
    type( field_type ), pointer :: cf_liq  => null()
    type( field_type ), pointer :: cf_bulk  => null()
    type( field_type ), pointer :: rh_crit => null()
    type( field_type ), pointer :: tau_dec_bm => null()
    type( field_type ), pointer :: tau_hom_bm => null()
    type( field_type ), pointer :: tau_mph_bm => null()

    type( field_type ), pointer :: dt_conv => null()
    type( field_type ), pointer :: du_conv => null()
    type( field_type ), pointer :: dv_conv => null()

    type( field_type ), pointer :: lw_down_surf => null()
    type( field_type ), pointer :: sw_down_surf => null()
    type( field_type ), pointer :: sw_up_tile   => null()

    type( field_type ), pointer :: tile_fraction        => null()
    type( field_type ), pointer :: leaf_area_index      => null()
    type( field_type ), pointer :: canopy_height        => null()
    type( field_type ), pointer :: sea_ice_thickness   => null()
    type( field_type ), pointer :: sea_ice_temperature => null()
    type( field_type ), pointer :: tile_temperature   => null()
    type( field_type ), pointer :: canopy_water       => null()
    type( field_type ), pointer :: tile_heat_flux => null()
    type( field_type ), pointer :: tile_moisture_flux => null()
    type( field_type ), pointer :: alpha1_tile => null()
    type( field_type ), pointer :: ashtf_prime_tile => null()
    type( field_type ), pointer :: dtstar_tile => null()
    type( field_type ), pointer :: fraca_tile => null()
    type( field_type ), pointer :: z0h_tile => null()
    type( field_type ), pointer :: z0m_tile => null()
    type( field_type ), pointer :: rhokh_tile => null()
    type( field_type ), pointer :: chr1p5m_tile => null()
    type( field_type ), pointer :: resfs_tile => null()
    type( field_type ), pointer :: canhc_tile => null()
    type( field_type ), pointer :: ustar => null()
    type( field_type ), pointer :: tile_water_extract => null()
    type( field_type ), pointer :: surf_interp_w2 => null()
    type( field_type ), pointer :: tau_land_w2 => null()
    type( field_type ), pointer :: tau_ssi_w2 => null()
    type( field_type ), pointer :: wspd10m => null()
    type( field_type ), pointer :: taux_ssi => null()
    type( field_type ), pointer :: tauy_ssi => null()

    type( field_type ), pointer :: peak_to_trough_orog  => null()
    type( field_type ), pointer :: silhouette_area_orog => null()

    type( field_type ), pointer :: soil_temperature   => null()
    type( field_type ), pointer :: soil_moist_avail => null()

    type( field_type ), pointer :: tile_snow_mass     => null()
    type( field_type ), pointer :: n_snow_layers      => null()
    type( field_type ), pointer :: snow_depth         => null()

    type( field_type ), pointer :: dA => null()
    type( field_type ), pointer :: height_w1 => null()
    type( field_type ), pointer :: height_w2 => null()
    type( field_type ), pointer :: height_w3 => null()
    type( field_type ), pointer :: height_wth => null()
    type( field_type ), pointer :: rdz => null()
    type( field_type ), pointer :: dtrdz => null()

    type( field_type ) :: du_conv_w2, dw_conv
    type( field_type ) :: dissip, diss_u, diss_v, diss_w
    type( field_type ) :: wind10m_w2, u10m, v10m, w10m
    type( field_type ) :: dmv_bl, dt_bl, tauz_ssi

    integer(kind=i_def) :: mesh_id

    if ( subroutine_timers ) call timer("bl_imp_alg")

    call log_event( 'Running implicit Boundary layer', LOG_LEVEL_INFO )

    mesh_id = theta%get_mesh_id()

    ! Unpack derived fields
    exner_in_wth => derived_fields%get_field('exner_in_wth')
    wetrho_in_wth => derived_fields%get_field('wetrho_in_wth')
    wetrho_in_w3 => derived_fields%get_field('wetrho_in_w3')
    wetrho_in_w2  => derived_fields%get_field('wetrho_in_w2')
    theta_star => derived_fields%get_field('theta_star')
    u_physics => derived_fields%get_field('u_physics')
    u_physics_star => derived_fields%get_field('u_physics_star')

    ! Unpack turbulence fields
    zh => turbulence_fields%get_field('zh')
    zhsc => turbulence_fields%get_field('zhsc')
    ntml => turbulence_fields%get_field('ntml')
    cumulus => turbulence_fields%get_field('cumulus')
    blend_height_tq => turbulence_fields%get_field('blend_height_tq')
    zh_nonloc => turbulence_fields%get_field('zh_nonloc')
    bl_type_ind => turbulence_fields%get_field('bl_type_ind')
    z_lcl => turbulence_fields%get_field('z_lcl')
    inv_depth => turbulence_fields%get_field('inv_depth')
    qcl_at_inv_top => turbulence_fields%get_field('qcl_at_inv_top')
    rhokh_bl => turbulence_fields%get_field('rhokh_bl')
    bq_bl => turbulence_fields%get_field('bq_bl')
    bt_bl => turbulence_fields%get_field('bt_bl')
    moist_flux_bl => turbulence_fields%get_field('moist_flux_bl')
    heat_flux_bl => turbulence_fields%get_field('heat_flux_bl')
    dtrdz_tq_bl => turbulence_fields%get_field('dtrdz_tq_bl')
    rdz_tq_bl => turbulence_fields%get_field('rdz_tq_bl')
    dsldzm  => turbulence_fields%get_field('dsldzm')
    wvar    => turbulence_fields%get_field('wvar')
    gradrinr => turbulence_fields%get_field('gradrinr')
    rhokm_w2 => turbulence_fields%get_field('rhokm_w2')
    tau_w2 => turbulence_fields%get_field('tau_w2')

    ! Unpack cloud fields
    cf_area => cloud_fields%get_field('area_fraction')
    cf_ice => cloud_fields%get_field('ice_fraction')
    cf_liq => cloud_fields%get_field('liquid_fraction')
    cf_bulk => cloud_fields%get_field('bulk_fraction')
    rh_crit => cloud_fields%get_field('rh_crit')

    tau_dec_bm => cloud_fields%get_field('tau_dec_bm')
    tau_hom_bm => cloud_fields%get_field('tau_hom_bm')
    tau_mph_bm => cloud_fields%get_field('tau_mph_bm')

    ! Unpack the convection
    dt_conv => convection_fields%get_field('dt_conv')
    du_conv => convection_fields%get_field('du_conv')
    dv_conv => convection_fields%get_field('dv_conv')

    ! Radiation fields
    lw_down_surf => radiation_fields%get_field('lw_down_surf')
    sw_down_surf => radiation_fields%get_field('sw_down_surf')
    sw_up_tile   => radiation_fields%get_field('sw_up_tile')

    ! Orography fields
    peak_to_trough_orog  => orography_fields%get_field('peak_to_trough_orog')
    silhouette_area_orog => orography_fields%get_field('silhouette_area_orog')

    ! Surface fields
    tile_fraction   => surface_fields%get_field('tile_fraction')
    leaf_area_index => surface_fields%get_field('leaf_area_index')
    canopy_height   => surface_fields%get_field('canopy_height')
    sea_ice_thickness => surface_fields%get_field('sea_ice_thickness')
    sea_ice_temperature => surface_fields%get_field('sea_ice_temperature')
    tile_temperature   => surface_fields%get_field('tile_temperature')
    canopy_water       => surface_fields%get_field('canopy_water')
    tile_heat_flux => surface_fields%get_field('tile_heat_flux')
    tile_moisture_flux => surface_fields%get_field('tile_moisture_flux')
    alpha1_tile => surface_fields%get_field('alpha1_tile')
    ashtf_prime_tile => surface_fields%get_field('ashtf_prime_tile')
    dtstar_tile => surface_fields%get_field('dtstar_tile')
    fraca_tile => surface_fields%get_field('fraca_tile')
    z0h_tile => surface_fields%get_field('z0h_tile')
    z0m_tile => surface_fields%get_field('z0m_tile')
    rhokh_tile => surface_fields%get_field('rhokh_tile')
    chr1p5m_tile => surface_fields%get_field('chr1p5m_tile')
    resfs_tile => surface_fields%get_field('resfs_tile')
    canhc_tile => surface_fields%get_field('canhc_tile')
    ustar => surface_fields%get_field('ustar')
    tile_water_extract => surface_fields%get_field('tile_water_extract')
    surf_interp_w2 => surface_fields%get_field('surf_interp_w2')
    tau_land_w2 => surface_fields%get_field('tau_land_w2')
    tau_ssi_w2 => surface_fields%get_field('tau_ssi_w2')
    wspd10m => surface_fields%get_field('wspd10m')
    taux_ssi => surface_fields%get_field('taux_ssi')
    tauy_ssi => surface_fields%get_field('tauy_ssi')

    ! Soil fields
    soil_temperature   => soil_fields%get_field('soil_temperature')
    soil_moist_avail => soil_fields%get_field('soil_moist_avail')

    ! Snow fields
    tile_snow_mass     => snow_fields%get_field('tile_snow_mass')
    n_snow_layers      => snow_fields%get_field('n_snow_layers')
    snow_depth         => snow_fields%get_field('snow_depth')

    height_wth => get_height(Wtheta, mesh_id)
    height_w1 => get_height(W1, mesh_id)
    height_w2 => get_height(W2, mesh_id)
    height_w3 => get_height(W3, mesh_id)
    dA => get_da_at_w2(mesh_id)
    rdz => get_rdz_fd1(mesh_id)
    dtrdz => get_dtrdz_fd2(mesh_id)

    ! Local surface fields that need creating
    call tile_temperature%copy_field_properties(snow_sublimation)
    call tile_temperature%copy_field_properties(surf_heat_flux)
    call tile_temperature%copy_field_properties(canopy_evap)
    call tile_temperature%copy_field_properties(total_snowmelt)
    call soil_temperature%copy_field_properties(water_extraction)

    ! Set up the BL increments
    call theta%copy_field_properties(dtheta_bl)
    call mr(imr_v)%copy_field_properties(dmv_bl)

    ! Map the convection wind increments into w2 space
    call u_physics%copy_field_properties(du_conv_w2)
    if ( cv_scheme == cv_scheme_gregory_rowntree ) then
      if ( subroutine_timers ) call timer("bl_imp_alg")
      call theta%copy_field_properties(dw_conv)
      call invoke(setval_c(dw_conv, 0.0_r_def) )
      call set_wind(du_conv_w2, du_conv, dv_conv, dw_conv)
      ! Set wind provides output in dynamics quantites, need to remove
      ! dA scaling for physics
      call invoke(inc_X_divideby_Y(du_conv_w2, dA) )
      if ( subroutine_timers ) call timer("bl_imp_alg")
    else
      call invoke(setval_c(du_conv_w2, 0.0_r_def) )
    end if

    ! Set up required variables in w2 space
    call u_physics%copy_field_properties(dissip)
    call exner%copy_field_properties(diss_u)
    call exner%copy_field_properties(diss_v)
    call exner%copy_field_properties(diss_w)
    call tau_ssi_w2%copy_field_properties(wind10m_w2)

    call invoke(set_any_dof_kernel_type(du_bl, W, 0.0_r_def),                  &
                set_any_dof_kernel_type(du_bl, S, 0.0_r_def),                  &
                set_any_dof_kernel_type(du_bl, E, 0.0_r_def),                  &
                set_any_dof_kernel_type(du_bl, N, 0.0_r_def),                  &
                setval_c(dissip, 0.0_r_def),                                   &
                setval_c(wind10m_w2, 0.0_r_def),                               &

                ! LFRic calculation of du on cell faces
                bl_imp_du_kernel_type(outer, du_bl, dissip, tau_w2, wind10m_w2,&
                                      tau_land_w2, tau_ssi_w2, rhokm_w2,       &
                                      rdz, dtrdz, wetrho_in_w2, u_physics,     &
                                      u_physics_star, surf_interp_w2,          &
                                      du_conv_w2, dA,                          &
                                      height_w1, height_w2),                   &
                ! Map the wind back into dynamics quantities
                inc_X_times_Y(du_bl, dA),                                      &
                inc_X_times_Y(dissip, dA) )

    if (fric_heating) then
      ! Calculate dissipation rates at cell centre
      call map_physics_winds(diss_u, diss_v, diss_w, dissip)
    end if

                ! Save vapour value for diagnostic increment
    call invoke(a_times_X(dmv_bl, -1.0_r_def, mr(imr_v)),                      &

                ! Call the implicit scheme for scalars
                bl_imp_kernel_type( outer, theta, wetrho_in_w3,                &
                           wetrho_in_wth, exner, exner_in_wth,                 &
                           mr_n(imr_v), mr_n(imr_cl), mr_n(imr_ci), theta_star,&
                           height_w3, height_wth, ntml, cumulus,               &
                           tile_fraction, leaf_area_index, canopy_height,      &
                           peak_to_trough_orog, silhouette_area_orog,          &
                           sea_ice_thickness, sea_ice_temperature,             &
                           tile_temperature, tile_snow_mass, n_snow_layers,    &
                           snow_depth, canopy_water, soil_temperature,         &
                           tile_heat_flux, tile_moisture_flux,                 &
                           sw_up_tile, sw_down_surf, lw_down_surf,             &
                           snow_sublimation, surf_heat_flux, canopy_evap,      &
                           water_extraction, total_snowmelt,                   &
                           dtheta_bl, diss_u, diss_v, dt_conv,                 &
                           mr(imr_v), mr(imr_cl), mr(imr_ci),                  &
                           cf_area, cf_ice, cf_liq, cf_bulk, rh_crit,          &
                           dsldzm, wvar, gradrinr, tau_dec_bm, tau_hom_bm,     &
                           tau_mph_bm,                                         &
                           rhokh_bl, bq_bl, bt_bl, moist_flux_bl, heat_flux_bl,&
                           dtrdz_tq_bl, rdz_tq_bl,                             &
                           alpha1_tile, ashtf_prime_tile, dtstar_tile,         &
                           fraca_tile, z0h_tile, z0m_tile, rhokh_tile,         &
                           chr1p5m_tile, resfs_tile, canhc_tile,               &
                           tile_water_extract, z_lcl, inv_depth,               &
                           qcl_at_inv_top, blend_height_tq, ustar,             &
                           soil_moist_avail, zh_nonloc, zh, zhsc, bl_type_ind),&

                ! Calculate diagnostic vapour increment
                inc_X_plus_Y(dmv_bl, mr(imr_v)) )

    if ( subroutine_timers ) call timer("bl_imp_alg")

    ! Output diagnostics on last iteration only
    if (write_diag .and. use_xios_io .and. outer == outer_iterations) then
      if ( subroutine_timers ) call timer("bl_imp_alg_xios")

      ! BL diagnostics
      call dtheta_bl%copy_field_properties(dt_bl)
      call invoke(    X_times_Y(dt_bl,dtheta_bl,exner_in_wth), &
                  inc_X_minus_Y(dt_bl,dt_conv) )
      call dt_bl%write_field('dt_bl')
      call dmv_bl%write_field('dmv_bl')
      call write_vector_diagnostic('du_bl',du_bl,clock,du_bl%get_mesh_id(),.false.)

      call zh%write_field('zh')

      ! Surface diagnostics
      call tile_heat_flux%write_field('tile_heat_flux')
      call tile_moisture_flux%write_field('tile_moisture_flux')
      call tile_temperature%write_field('tile_temperature')
      call tile_fraction%write_field('tile_fraction')
      call canopy_evap%write_field('canopy_evap')

      call zh%copy_field_properties(u10m)
      call zh%copy_field_properties(v10m)
      call zh%copy_field_properties(w10m)
      ! Calculate zonal and meridional wind speeds
      call map_physics_winds(u10m, v10m, w10m, wind10m_w2)
      call u10m%write_field('u10m')
      call v10m%write_field('v10m')
      ! Calculate the 10m windspeed
      call invoke(inc_X_powint_n(u10m, 2_i_def),    &
                  inc_X_powint_n(v10m, 2_i_def),    &
                  X_plus_Y(wspd10m,u10m,v10m),      &
                  inc_X_powreal_a(wspd10m, 0.5_r_def) )
      call wspd10m%write_field('wspd10m')

      call zh%copy_field_properties(tauz_ssi)
      ! Calculate zonal and meridional wind stresses
      call map_physics_winds(taux_ssi, tauy_ssi, tauz_ssi, tau_ssi_w2)
      call taux_ssi%write_field('taux_ssi')
      call tauy_ssi%write_field('tauy_ssi')

      if ( subroutine_timers ) call timer("bl_imp_alg_xios")
    end if

  end subroutine bl_imp_alg

end module bl_imp_alg_mod
