!-----------------------------------------------------------------------------
! (C) Crown copyright 2022 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> Tests serial logging with a clock.
!>
!> This also tests parallel logging with a single rank as it is treated the
!> same.
!>
module log_mod_serial_clock_test

  use constants_mod, only : i_native, i_timestep
  use log_mod,       only : finalise_logging,     &
                            initialise_logging,   &
                            log_event,            &
                            log_forget_timestep,  &
                            log_level_info,       &
                            log_level_warning,    &
                            log_set_alert_stream, &
                            log_set_info_stream,  &
                            log_set_level,        &
                            log_set_timestep
  use pFUnit_Mod

  implicit none

  private
  public :: log_clock_test, log_clock_serial_test

  integer, parameter :: info_unit  = 12
  integer, parameter :: alert_unit = 13

  @testCase
  type, public, extends(TestCase) :: log_test_clock_serial_type
    private
  contains
    private
    procedure, public :: log_clock_test
  end type log_test_clock_serial_type

contains

  !> Tests the addition and removal of a clock in a serial environment.
  !>
  @test
  subroutine log_clock_test( this )

    implicit none

    class(log_test_clock_serial_type), intent(inout) :: this

    call initialise_logging()
    call log_clock_serial_test()
    call finalise_logging()

  end subroutine log_clock_test


  subroutine log_clock_serial_test

    implicit none

    integer(i_native)   :: condition
    character(8)        :: info_tag, alert_tag
    character(80)       :: info_message, alert_message
    character(1)        :: info_field, alert_field
    integer(i_timestep) :: info_step, alert_step

    open( info_unit, status='scratch', action='readwrite', &
          iostat=condition )
    @assertEqual( 0, condition )
    open( alert_unit, status='scratch', action='readwrite', &
          iostat=condition )
    @assertEqual( 0, condition )

    call log_set_level( log_level_info )
    call log_set_info_stream( info_unit )
    call log_set_alert_stream( alert_unit )
    call log_forget_timestep()

    call log_event( "No clock", log_level_info )
    call log_event( "Clock none", log_level_warning )

    call log_set_timestep( 1 )

    call log_event( "First step", log_level_info )
    call log_event( "Step the first", log_level_warning )

    call log_set_timestep( 2 )

    call log_event( "Second step", log_level_info )
    call log_event( "Step the second", log_level_warning )

    call log_forget_timestep()

    call log_event( "No clock", log_level_info )
    call log_event( "Clock none", log_level_warning )

    rewind( info_unit )
    rewind( alert_unit )
    read( info_unit,  '(23X,A8,A)' ) info_tag, info_message
    read( alert_unit, '(23X,A8,A)' ) alert_tag, alert_message

    @assertEqual( ':INFO :', info_tag )
    @assertEqual( "No clock", info_message )
    @assertEqual( ':WARN :', alert_tag )
    @assertEqual( "Clock none", alert_message )

    read( info_unit,  &
          '(24X,A1,I1,A8,A)' ) info_field, info_step, info_tag, info_message
    read( alert_unit, &
          '(24X,A1,I1,A8,A)' ) alert_field, alert_step, alert_tag, alert_message

    @assertEqual( 'S', info_field )
    @assertEqual( 1, info_step )
    @assertEqual( ':INFO :', info_tag )
    @assertEqual( "First step", info_message )
    @assertEqual( 'S', alert_field )
    @assertEqual( 1, alert_step )
    @assertEqual( ':WARN :', alert_tag )
    @assertEqual( "Step the first", alert_message )

    read( info_unit,  &
          '(24X,A1,I1,A8,A)' ) info_field, info_step, info_tag, info_message
    read( alert_unit, &
          '(24X,A1,I1,A8,A)' ) alert_field, alert_step, alert_tag, alert_message

    @assertEqual( 'S', info_field )
    @assertEqual( 2, info_step )
    @assertEqual( ':INFO :', info_tag )
    @assertEqual( "Second step", info_message )
    @assertEqual( 'S', alert_field )
    @assertEqual( 2, alert_step )
    @assertEqual( ':WARN :', alert_tag )
    @assertEqual( "Step the second", alert_message )

    read( info_unit,  '(23X,A8,A)' ) info_tag, info_message
    read( alert_unit, '(23X,A8,A)' ) alert_tag, alert_message

    @assertEqual( ':INFO :', info_tag )
    @assertEqual( "No clock", info_message )
    @assertEqual( ':WARN :', alert_tag )
    @assertEqual( "Clock none", alert_message )

    close( alert_unit, iostat=condition )
    @assertEqual( 0, condition )
    close( info_unit, iostat=condition )
    @assertEqual( 0, condition )

  end subroutine log_clock_serial_test

end module log_mod_serial_clock_test
